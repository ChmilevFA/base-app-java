plugins {
    id 'nu.studer.jooq' version "$jooqPluginVersion"
    id 'java'
}

dependencies {
    implementation "org.flywaydb:flyway-core:$flywayVersion"
    implementation "org.postgresql:postgresql:$postgresqlVersion"
    implementation "org.jooq:jooq:$jooqVersion"

    jooqGenerator "com.github.sabomichal:jooq-meta-postgres-flyway:$jooqMetaPostgresFlywayVersion"
}

jooq {
    configurations {
        main {
            generateSchemaSourceOnCompilation = true
            generationTool {
                generator {
                    database {
                        name = "com.github.sabomichal.jooq.PostgresDDLDatabase"
                        inputSchema = "public"
                        includes = "public.*"
                        excludes = "flyway_schema_history"
                        recordTimestampFields = "UPDATED_DATE"
                        properties {
                            property {
                                key = "locations"
                                value = "src/main/resources/net/chmilevfa/templates/base/db/migration"
                            }
                            property {
                                key = "dockerImage"
                                value = "postgres:15"
                            }
                        }
                    }
                    target {
                        packageName = 'net.chmilevfa.templates.base.db.schema'
                        directory = 'src/generated/java'
                    }
                }
            }
        }
    }
}

clean {
    delete file('src/generated/java')
}